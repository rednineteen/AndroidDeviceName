apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
}

task clearData(type: Exec) {
    def clearDataCommand = ['adb', 'shell', 'pm', 'clear', 'com.rednineteen.android.adn.test']
    commandLine clearDataCommand
}

// This is the library version used when deploying the artifact
version = android.defaultConfig.versionName
group = "com.rednineteen.android"                                   // Maven Group ID for the artifact
def siteUrl = 'https://github.com/rednineteen/AndroidDeviceName'    // Homepage URL of the library
def gitUrl = 'https://github.com/rednineteen/AndroidDeviceName.git' // Git repository URL

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'com.rednineteen.android:adn'
                description = 'Android Device Name library'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'velval'
                        name 'Juan Velasquez'
                        email 'juan@rednineteen.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']

    pkg {
        repo = 'Android'
        name = 'com.rednineteen.android:adn'
        desc = 'Android Device Name library'
        userOrg = 'red19'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        publish = true

        version {
            name = android.defaultConfig.versionName
            desc = 'Android Device Name library'
            released  = new Date()
            vcsTag = android.defaultConfig.versionName
        }
    }
}